plugins {
    id "java-library"
    id "org.openmicroscopy.project" version "5.5.1"
    id "org.openmicroscopy.gradle.ice-builder.slice" version "1.5.1-SNAPSHOT"
}

group = "org.openmicroscopy"
version = "5.5.3-SNAPSHOT"

repositories {
    mavenLocal()
    jcenter()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

ext {
    //the database type: must match the version set in omero-model
    databaseType = "psql"
    generatedDir = "build/${databaseType}"
}

if (slice.iceVersion.contains("3.7")) {
    slice.output = file("$generatedDir/ice37")
    sourceSets.main.java.srcDirs("src/main/ice37", slice.output)
} else if (slice.iceVersion.contains("3.6")) {
    slice.output = file("$generatedDir/ice36")
    sourceSets.main.java.srcDirs("src/main/ice36", slice.output)
}

dependencies {
    testImplementation("junit:junit:4.12")
    testImplementation("org.testng:testng:6.14.2")
    testImplementation("jmock:jmock:1.+")
    testImplementation("org.apache.directory.server:apacheds-all:1.5.7")
    testImplementation('org.openmicroscopy:omero-common-test:5.5.1')
    testImplementation('org.slf4j:slf4j-log4j12:1.5.0')
    configurations.testCompile.exclude(group: 'org.slf4j', module: 'jcl-over-slf4j')


    // https://mvnrepository.com/artifact/org.apache.directory.shared/shared-ldap
    // testCompile group: 'org.apache.directory.shared', name: 'shared-ldap-constants', version: '1.0.0-M1'
    // testCompile group: 'org.apache.directory.shared', name: 'shared-ldap', version: '0.9.15'

    api('org.openmicroscopy:omero-renderer:5.5.1')

    implementation("com.zeroc:ice")

    // Spring framework stuff
    implementation("org.springframework:spring-context-support:4.3.14.RELEASE")
    implementation("org.springframework:spring-jms:4.3.14.RELEASE")
    implementation("org.springframework.security:spring-security-ldap:4.2.4.RELEASE")

    if (databaseType.equals("psql")) {
        // Postgres connect driver for java
        implementation 'org.postgresql:postgresql:42.2.1'
    }
    // Used by hibernate, see: https://stackoverflow.com/a/14365438
    implementation("org.javassist:javassist:3.25.0-GA")

    // API monitoring thing: http://jamonapi.sourceforge.net
    implementation("com.jamonapi:jamon:2.81")

    implementation("com.github.marcus-nl.btm:btm:3.0.0-mk1")

    implementation("org.apache.pdfbox:pdfbox:2.0.8")
    implementation("org.apache.commons:commons-collections4:4.1")
    implementation("org.apache.xmlgraphics:batik-all:1.9.1")

    implementation("commons-beanutils:commons-beanutils:1.9.3")

    implementation("com.sun.mail:javax.mail:1.6.1")

    implementation("com.mortennobel:java-image-scaling:0.8.6")

    implementation("org.quartz-scheduler:quartz:2.2.1")

    implementation("org.subethamail:subethasmtp:3.1.7")

    implementation("net.sf.ehcache:ehcache:2.10.4")
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG() {
        suites "src/test/resources/unit.testng.xml"
    }
}

clean {
    delete generatedDir
}


jar {
    manifest {
        attributes("Main-Class": 'ome.services.fulltext.Main')
    }
}

processResources {
    with copySpec {
        from 'src/main/resources'
        // Set version
        filter { String line ->
            line.startsWith('omero.version') ? 'omero.version=' + version : line
        }
    }
}

task compileIcedoc(type: org.openmicroscopy.tasks.IceDocsTask, dependsOn: "compileSlice") {
    source = "src/main/slice"
    outputDir = file("$buildDir/docs/icedoc")
}

task zipIcedoc(type: Zip) {
    archiveClassifier.set("icedoc")
    destinationDirectory.set(layout.buildDirectory.dir("libs"))
    from compileIcedoc
}

publishing {
    publications.getByName("omeroServer") { MavenPublication pub ->
        // Ensure ice docs is the last element in maven-metadata.xml
        // (located in .m2/org/openmicroscopy/omero-server)
        pub.artifact zipIcedoc
    }
}