task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

publishing {
    publications {
        serverJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            credentials {
                username = System.getenv('ARTIFACTORY_USER') ?: "${findProperty('artifactory_user')}"
                password = System.getenv('ARTIFACTORY_PASSWORD') ?: "${findProperty('artifactory_password')}"
            }
            if (version.endsWith('SNAPSHOT')) {
                url "https://artifacts.openmicroscopy.org/artifactory/ome.snapshots"
            } else {
                url "https://artifacts.openmicroscopy.org/artifactory/ome.staging"
            }
        }
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}